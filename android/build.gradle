import groovy.json.JsonSlurper

static File locateOpeniapVersionsFile(File pluginDir, File hostProjectRoot) {
    // First try to find in host app's root directory
    File candidate = new File(hostProjectRoot, 'openiap-versions.json')
    if (candidate.isFile()) {
        return candidate
    }
    // Search upwards from plugin directory (includes plugin's android directory)
    File current = pluginDir
    while (current != null) {
        candidate = new File(current, 'openiap-versions.json')
        if (candidate.isFile()) {
            return candidate
        }
        current = current.getParentFile()
    }
    throw new GradleException(
        "Unable to locate openiap-versions.json. Searched in: ${hostProjectRoot.absolutePath}, ${pluginDir.absolutePath}, and parent directories"
    )
}

def openiapVersionsFile = locateOpeniapVersionsFile(rootDir, project.rootProject.rootDir)
def openiapVersions = new JsonSlurper().parse(openiapVersionsFile)
def openiapGoogleVersion = openiapVersions['google']

group 'dev.hyo.flutterinapppurchase'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.8.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    if (project.android.hasProperty('namespace')) {
        namespace 'dev.hyo.flutterinapppurchase'
    }
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    // OpenIAP Google billing wrapper includes Google Play Billing
    implementation "io.github.hyochan.openiap:openiap-google:${openiapGoogleVersion}"

    // For local debugging with a checked-out module, you can switch to:
    //   debugImplementation project(":openiap")
    // and include the module in android/settings.gradle (see CONTRIBUTING.md)

    implementation files('jars/in-app-purchasing-2.0.76.jar')
    implementation 'androidx.annotation:annotation:1.6.0'
}
